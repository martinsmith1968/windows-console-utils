@ECHO OFF

SETLOCAL EnableDelayedExpansion

SET BASEPATH=%~1
SET APPNAME=%~2
SET APPDESCRIPTION=%~3
SET APPFILENAME=%~4
SET EXEFILENAME=%~5
SET ICONFILENAME=%~6
SET COMPANY=%~7
SET COPYRIGHT=%~8

IF "%COMPANY%" == "" SET COMPANY=DNX Solutions Ltd
IF "%COPYRIGHT%" == "" SET COPYRIGHT=(c) 2019 %COMPANY%


IF NOT EXIST "%APPFILENAME%" (
	ECHO.App script filename not found : %APPFILENAME%
	GOTO :EOF
)

CALL FINDAPP.CMD AutoHotKey Compiler Ahk2Exe.exe
SET AHKCOMPILEREXE=%APP%
IF NOT EXIST "%APP%" (
    ECHO.AHK Compiler not available
    GOTO :EOF
)

ECHO.Killing %EXEFILENAME%
PSKILL "%EXEFILENAME%"

IF EXIST "%EXEFILENAME%" (
	ECHO.Removing %EXEFILENAME%...
	DEL /Q "%EXEFILENAME%"
)

ECHO.Building %APPNAME%...
"%AHKCOMPILEREXE%" /in "%APPFILENAME%" /icon "%ICONFILENAME%"

IF EXIST "%EXEFILENAME%" (
	ECHO.Version Patching %EXEFILENAME%...
	VERPATCH "%EXEFILENAME%" /s company "%COMPANY%" /s desc "%APPDESCRIPTION%" /s product "%APPNAME%" /s copyright "%COPYRIGHT%"
)

PAUSEN -t 5

@ECHO OFF

SETLOCAL

SET POS=1
SET FOLDER=
SET MOVE=N
SET STRATEGY=NUMERICSUFFIX
SET FOLDERPREFIX_TEXT=Backup-
SET NUMERICSUFFIX_TEXT=

:PARSE
IF /I "%~1" == "/m" (
  SET MOVE=Y
  SHIFT && GOTO :PARSE
)
IF /I "%~1" == "-m" (
  SET MOVE=Y
  SHIFT && GOTO :PARSE
)
IF /I "%~1" == "-SNS" (
  SET STRATEGY=NUMERICSUFFIX
  SHIFT && GOTO :PARSE
)
IF /I "%~1" == "/SNS" (
  SET STRATEGY=NUMERICSUFFIX
  SHIFT && GOTO :PARSE
)
IF /I "%~1" == "-SBP" (
  SET STRATEGY=BACKUPPREFIX
  SHIFT && GOTO :PARSE
)
IF /I "%~1" == "/SBP" (
  SET STRATEGY=BACKUPPREFIX
  SHIFT && GOTO :PARSE
)
IF /I "%~1" == "/NST" (
  SET NUMERICSUFFIX_TEXT=%~2
  SHIFT && SHIFT && GOTO :PARSE
)
IF /I "%~1" == "-NST" (
  SET NUMERICSUFFIX_TEXT=%~2
  SHIFT && SHIFT && GOTO :PARSE
)
IF /I "%~1" == "/FPT" (
  SET FOLDERPREFIX_TEXT=%~2
  SHIFT && SHIFT && GOTO :PARSE
)
IF /I "%~1" == "-FPT" (
  SET FOLDERPREFIX_TEXT=%~2
  SHIFT && SHIFT && GOTO :PARSE
)

IF NOT "%~1" == "" (
  IF %POS% == 1 SET FOLDER=%~1

  SHIFT && GOTO :PARSE
)

:VALIDATE
IF "%FOLDER%" == "" GOTO :USAGE

IF NOT EXIST "%FOLDER%\*.*" (
  CALL :ERROR Folder not found : %FOLDER%
  GOTO :USAGE
)

:SANITISE
SET TRAILING=%FOLDER:~-1%

IF NOT "%TRAILING%" == "\" GOTO :GO

SET FOLDER=%FOLDER:~0,-1%
GOTO :SANITISE

:GO
CALL :GETNEWFOLDERNAME "%FOLDER%" "%STRATEGY%"

IF NOT EXIST "%NEWFOLDER%\*.*" (
  ECHO.Creating: %NEWFOLDER%
  MKDIR "%NEWFOLDER%" > NUL
)

IF "%MOVE%" == "N" (
  COPY /Y /V "%FOLDER%" "%NEWFOLDER%" > NUL
) ELSE (
  MOVE /Y "%FOLDER%" "%NEWFOLDER%" > NUL
)

GOTO :EOF


:GETNEWFOLDERNAME
SET NEWFOLDER=

IF "%~1" == "" GOTO :EOF
IF "%~2" == "" GOTO :EOF

CALL :GETNEWFOLDERNAME_%~2 "%~1"

GOTO :EOF


:GETNEWFOLDERNAME_NUMERICSUFFIX
SET INDEX=1

:GETNEWFOLDERNAME_NUMERICSUFFIX_LOOP
SET NEWFOLDER=%~1%NUMERICSUFFIX_TEXT%%INDEX%

IF EXIST "%NEWFOLDER%\*.*" (
  SET /A INDEX+=1
  GOTO :GETNEWFOLDERNAME_NUMERICSUFFIX_LOOP
)

GOTO :EOF


:GETNEWFOLDERNAME_BACKUPPREFIX
SET NEWFOLDER=%~1

:GETNEWFOLDERNAME_BACKUPPREFIX_LOOP
SET NEWFOLDER=%FOLDERPREFIX_TEXT%%NEWFOLDER%

IF EXIST "%NEWFOLDER%\*.*" (
  GOTO :GETNEWFOLDERNAME_BACKUPPREFIX_LOOP_LOOP
)

GOTO :EOF


:ERROR
ECHO.Error: %*
GOTO :EOF


:USAGE
ECHO.%~n0 - Backup a folder to a new name
ECHO.
ECHO.%~n0 [folder] [options]
ECHO.
ECHO.Options:
ECHO.
ECHO./M   - Move instead of Copy
ECHO./SNS - Use Numeric Suffix strategy
ECHO./SBP - Use Backup- prefix stragegy

GOTO :EOF

@ECHO OFF

SETLOCAL

SET SCRIPTPATH=%~dp0
SET SCRIPTNAME=%~n0
SET SCRIPTFULLFILENAME=%~dpnx0

SET BACKUPEXTENSION=.bacpac

SET CONNECTION=localhost

IF "%SQLCMD_EXE%" == "" SET SQLCMD_EXE=SQLCMD.EXE
IF "%SQLPACKAGE_EXE%" == "" SET SQLPACKAGE_EXE=SQLPACKAGE.EXE

SET FILENAME=%~1
SET DATABASE=%~2

IF "%DATABASE%" == "" SET DATABASE=%~n1

IF "%FILENAME%" == "" (
    CALL :USAGE
    GOTO :EOF
)

IF NOT EXIST "%FILENAME%" (
    CALL :USAGE
    CALL :ERROR "File not found: %FILENAME%"
    GOTO :EOF
)


ECHO.Dropping: %DATABASE% on %CONNECTION%...
%SQLCMD_EXE% -E -S "%CONNECTION%" -d master -Q "IF DB_ID('%DATABASE%') IS NOT NULL BEGIN; ALTER DATABASE [%DATABASE%] SET SINGLE_USER WITH ROLLBACK IMMEDIATE; DROP DATABASE [%DATABASE%]; END;"

ECHO.Restoring %DATABASE% from %FILENAME%
%SQLPACKAGE_EXE% /action:import /sf:"%FILENAME%" /p:Storage=File /tcs:"Data Source=%CONNECTION%;Initial Catalog=%DATABASE%;Trusted_Connection=True;"

ECHO.Creating Login...
%SQLCMD_EXE% -E -S "%CONNECTION%" -d "%DATABASE%" -Q "CREATE USER [NT AUTHORITY\NETWORK SERVICE] FOR LOGIN [NT AUTHORITY\NETWORK SERVICE]"

GOTO :EOF


:USAGE
ECHO.%SCRIPTNAME% - Restore a Db from a %BACKUPEXTENSION%
ECHO.
ECHO.Usage:
ECHO.%SCRIPTNAME% [filename:database-name%BACKUPEXTENSION%] { [database-name] }

IF EXIST "*%BACKUPEXTENSION%" (
    ECHO.
    ECHO.Databases:
    DIR /B *%BACKUPEXTENSION% | SED -e "s/\%BACKUPEXTENSION%$//g"
)

GOTO :EOF


:ERROR
ECHO.Error: %*

GOTO :EOF
